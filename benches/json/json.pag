lexer {
    definition DIGIT = '0'..'9';
    definition NONZERO = '1'..'9';
    definition HEX_DIGIT = '0' .. '9' | 'a' .. 'f' | 'A' .. 'F';
    definition ESCAPED = '"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't';

    token LBRACKET = '{';
    token RBRACKET = '}';
    token COMMA = ',';
    token COLON = ':';
    token LSQUARE = '[';
    token RSQUARE = ']';
    token TRUE = "true";
    token FALSE = "false";
    token NULL = "null";
    token STRING = '"' ~ ( !('\\' | '"') | '\\' ~ (ESCAPED | 'u' ~ HEX_DIGIT ~ HEX_DIGIT ~ HEX_DIGIT ~ HEX_DIGIT) )* ~ '"';
    token NUMBER = '-'? ~ ('0' | NONZERO ~ DIGIT*) ~ ('.' ~ DIGIT+)? ~ (('e' | 'E') ~ ('+' | '-')? ~ DIGIT+)?;

    skip = ('\n' | '\r' | '\t' | ' ')+;
}

parser json {
    active attribute =
        string ~ COLON ~ value;

    active string = STRING;
    active number = NUMBER;
    active lit_true = TRUE;
    active lit_false = FALSE;
    active lit_null = NULL;

    active object =
        LBRACKET ~ (attribute ~ (COMMA ~ attribute)*)? ~ RBRACKET;

    active array =
        LSQUARE ~ (value ~ (COMMA ~ value)*)? ~ RSQUARE;

    silent value = string | number | array | object | lit_true | lit_false | lit_null;

    active json = value;
}
