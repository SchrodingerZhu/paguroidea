lexer {
    definition DIGIT = '0'..'9';
    definition NONZERO = '1'..'9';
    definition HEX_DIGIT = '0' .. '9' | 'a' .. 'f' | 'A' .. 'F';
    definition ESCAPED = '"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't';

    silent token WHITESPACE = ('\n' | '\r' | '\t' | ' ')+;
    active token LBRACKET = '{';
    active token RBRACKET = '}';
    active token COMMA = ',';
    active token COLON = ':';
    active token LSQUARE = '[';
    active token RSQUARE = ']';
    active token TRUE = "true";
    active token FALSE = "false";
    active token NULL = "null";
    active token STRING = '"' ~ ( !('\\' | '"') | '\\' ~ (ESCAPED | 'u' ~ HEX_DIGIT ~ HEX_DIGIT ~ HEX_DIGIT ~ HEX_DIGIT) )* ~ '"';
    active token NUMBER = '-'? ~ ('0' | NONZERO ~ DIGIT*) ~ ('.' ~ DIGIT+)? ~ (('e' | 'E') ~ ('+' | '-')? ~ DIGIT+)?;
}

parser json {
    active attribute =
        string ~ COLON ~ value;

    active string = STRING;
    active number = NUMBER;
    active lit_true = TRUE;
    active lit_false = FALSE;
    active lit_null = NULL;

    active object =
        LBRACKET ~ (attribute ~ (COMMA ~ attribute)*)? ~ RBRACKET;

    active array =
        LSQUARE ~ (value ~ (COMMA ~ value)*)? ~ RSQUARE;

    silent value = string | number | array | object | lit_true | lit_false | lit_null;

    active json = value;
}
